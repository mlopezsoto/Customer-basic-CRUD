plugins {
	id 'org.springframework.boot' version '2.4.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'org.openapi.generator' version "5.1.1"
}

group = 'com.mlopezsoto'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
	jcenter()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation "io.springfox:springfox-boot-starter:3.0.0"

	implementation 'io.springfox:springfox-swagger-ui:3.0.0'

	// Code generation from OpenAPI yaml file
	implementation "io.swagger.parser.v3:swagger-parser:2.0.27"
	implementation "org.openapitools:jackson-databind-nullable:0.2.1"

	implementation "com.h2database:h2"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.mockito:mockito-core:3.11.1'
	testImplementation 'org.mockito:mockito-junit-jupiter:3.11.1'
}

test {
	useJUnitPlatform()
}

openApiGenerate {
	generatorName = "spring"
	inputSpec = "$projectDir/src/main/resources/services.yaml"
	outputDir = "$buildDir/generated"
	apiPackage = "com.mlopezsoto.customer.api"
	invokerPackage = "com.mlopezsoto.customer"
	modelPackage = "com.mlopezsoto.customer.model"
	configOptions = [
			dateLibrary: "java8",
			interfaceOnly: "true"
	]
}

compileJava.dependsOn tasks.openApiGenerate

sourceSets.main.java.srcDirs = ["$buildDir/generated/src/main/java", "$projectDir/src/main/java"]
sourceSets.main.resources.srcDirs = ["$buildDir/generated/src/main/resources", "$projectDir/src/main/resources"]

